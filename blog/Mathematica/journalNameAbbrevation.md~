Title: 期刊全名转缩写
Date: 2015-04-23 19:12:25
Categories: Mathematica
Tags: Mahtematica
Summary: 平时文献多是通过Google Scholar收集，导出为bib格式。这样导出的Journal文献其期刊名是全名，而正式发表的文章，期刊名多用缩写。这次写完大论文，需要把期刊名替换为缩写，虽然可以通过手动查找-替换的方法完成，但工作量依然很大。于是用Mathematica写了一个小程序，专门做这份繁琐的工作。

平时文献多是通过Google Scholar收集，导出为bib格式。
这样导出的Journal文献其期刊名是全名，而正式发表的文章，期刊名多用缩写。
这次写完大论文，需要把期刊名替换为缩写，虽然可以通过手动查找-替换的方法完成，但工作量依然很大。
于是用Mathematica写了一个小程序，专门做这份繁琐的工作。

基本的思路是：

1. 先到网上去获取一份期刊全名和缩写名对应的数据库，建立全名->缩写名对应的替换规则。
2. 匹配 bib 文献数据库的期刊名称，用上面建立的全名->缩写名规则替换。

### 数据库获取
在[UBC大学的图书馆](http://woodward.library.ubc.ca/research-help/journal-abbreviations/)网站下载一份包含期刊全名和对应缩写名的html源码。部分代码如下：
```html
...
<td>2D Mater.</td><td>2D Materials </td></tr><tr><td class="nav" \
colspan="2" style="text-align: center; background-color: rgb(255, \
187, 136);"><a id="3">3</a> <a href="#jatop" class="small" \
title="back to top" style="font-size: x-small;">top</a></td></tr><tr \
style="background-color: rgb(221, 221, 221);"><td>3D Print. Addit. \
Manuf.</td><td>3D Printing and Additive Manufacturing</td>
...
```
通过Mathematica强大的模式匹配提取了其中的期刊名称和对应缩写，并存储为二维数组，第一列为全名，第二列为其对应缩写。
观察html源码，最后分两种模式提取期刊名称，代码如下：

```mma
journalsPart1 = 
  Rest@StringCases[downloadedHtmlContent, 
    Shortest[
      "<tr style=\"background-color: rgb(221, 221, 221);\"><td>" ~~ 
       abbr___ ~~ "</td><td>" ~~ fullname___ ~~ 
       "</td></tr>"] -> {StringTrim@fullname, StringTrim@abbr}];

journalsPart2 = 
  Rest@StringCases[downloadedHtmlContent, 
    Shortest[
      "<tr><td>" ~~ abbr___ ~~ "</td><td>" ~~ fullname___ ~~ 
       "</td></tr>"] -> {StringTrim@fullname, StringTrim@abbr}];

fullnameAbbr = Join[journalsPart1, journalsPart2];
```
这个数据库总共有5758条期刊全名及其对应缩写，覆盖领域相当广泛。

### 建立替换规则
将以上抓取的数据建立全名->缩写名对应的替换规则，这步就非常简单了，代码如下：
```mma
journalToAbbrRules = Rule[
     First@#, Last@#
     ] & /@ fullnameAbbr;
```
### 运用替换规则
这里一开始使用的直接替换方法，即直接搜索待替换的文献数据库，当匹配到数据库中的期刊全名就将其替换掉。这种方法虽然思路直观但效率不高。代码如下：
```mma
refsToAbbr = StringReplace[yourRefs, journalToAbbrRules, 
  IgnoreCase -> True];
```
另一种优化的替换方式，注意到期刊名在bib文件中以journal = {"期刊名"}这种Pattern存在的，于是可以将替换方法优化为：
```mma
refsToAbbrOptimized = 
  StringReplace[yourRefs, 
   Shortest[
     "journal={" ~~ journalname___ ~~ "}"] :> ("journal={" ~~ 
      StringReplace[journalname, journalToAbbrRules, 
       IgnoreCase -> True] ~~ "}")];
```
以上两种方法，以我自己的100篇文献数据库为例，第一种方法耗时约26秒，第二种方法仅耗时约0.4秒。

### Try using Machine Learning
替换过程中发现一个小的bug，谷歌学术下载回来的PRB文献，其全称为：Physical Review B，而上面下载到的数据库，PRB的全名为：Phys. Rev. B: Condens. Matter。
于是按照上面的字符串匹配替换的方法，该条文献就会被忽略。

Mathematica 10新增了机器学习的函数，用法也非常简单。于是被我拿出来在此处把玩一番，因为是直接拿来运用，未经优化，可能效率不高，仅供参考。
首先将上面建立的替换规则，来机器学习一番，其实就是：
```mma
abbrDatabase = Classify[journalToAbbrRules]
```
于是我们可以发现，此时输入Physical Review B就会主动地去匹配数据库中的Phys. Rev. B: Condens. Matter这本杂志了：
```mma
In:= abbrDatabase["Physical Review B"]
Out:= "Phys. Rev. B: Condens. Matter"
```
